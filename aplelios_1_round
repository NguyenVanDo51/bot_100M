//+------------------------------------------------------------------+
//|                                                      ProjectName |
//|                                      Copyright 2020, CompanyName |
//|                                       http://www.companyname.net |
//+------------------------------------------------------------------+
#property copyright "Copyright 2024, Nguyen Long Nhat ft Nguyen Van Do"
#property link      "https://www.mql5.com"
#property version   "2.1"
#include <Trade\Trade.mqh>

string VERSION = "2.1";

enum BOT_MODE
  {
   MODE_68 = 1,
   MODE_69 = 2,
   MODE_75 = 12,
   SAFE_MODE_80 = 3,
   SUPER_SAFE_MODE_83 = 4,
   CUSTOM_MODE = 5
  };

enum AUTO_X_MODE
  {
   MODE_10K_X3_SL_42 = 12,
   MODE_3K_X1_SL_45 = 1,
   MODE_2K5_X1_SL_54 = 2,
   MODE_2K_X1_SL_70 = 3,
   CUSTOM = 0,
  };

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
input BOT_MODE botMode = MODE_68;               // MODE (Cài sẵn RSI, x2 lot với safe mode)
input AUTO_X_MODE autoX = MODE_2K_X1_SL_70;     // Hệ số tự đông
input bool compoundInterest = true;            // Chế độ lãi kép

input string _ = "                                                                ";
input int xBuyInput = 1;                        // Tùy chỉnh hệ số Sell
input int xSellInput = 1;                       // Tùy chỉnh hệ số Buy

input string __ = "                                                                ";
input double rsiSellInput = 69;                 // Ngưỡng RSI Sell
input double rsiBuyInput = 31;                  // Ngưỡng RSI Buy

input string ______ = "                                                                ";
input double TPRSIDistanceSellInput = 5;        // RSI hồi để đóng sớm Sell (69 - 8 = 61)
input double TPRSIDistanceBuyInput = 5;          // RSI hồi để đóng sớm Buy (31 + 8 = 39)

input string ___ = "                                                                ";
input double stepPip = 200;                     // Step pips và TP Pips

input string ____ = "                                                                ";
input int delaySeconds = 50;              // Delay time (s)
input int maxDelaySeconds = 180;          // Max Delay time (s)
input int delayTimeStep = 10;             // Delay time step

input int candleNonTP = 10;               // Số nến chốt non
input int candleTP0 = 15;                 // Số nến chốt hòa

double TPRSISell = 60;
double TPRSIBuy = 40;
//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
int xBuy = 1;                       // Hệ số Sell (10k = 3)
int xSell = 1;                      // Hệ số Buy (10k = 3)

input ENUM_TIMEFRAMES mainTimeframe = PERIOD_M5;
input int rsiPeriod = 14;           // Chu kỳ RSI

input string _____ = "                                                                ";
input int maxOrders = 12;           // Số lệnh tối đa
input int nonTPQuantity = 5;              // Số lệnh chốt non
input int averageQuantity = 7;            // Số lệnh chốt hòa


input string _______ = "                                                                ";
const string TG_API_URL = "https://api.telegram.org";
input string botTkn = "8143370585:AAHAYagRIB614EkjsA_8ggflIP-vlLJsnwY"; // bot telegram token
input string chatID = "-1002349691879"; // chatID

double tpPip;                             // Số pips chốt lời
double nonTpPip;                          // Số pips chốt lời sớm
double stoplossPip = stepPip;     // Số pips Stoploss


// Khai báo Magic Number
int MAGIC_NUMBER = 123456;
double lotSizes[] =
  {
   0.01, 0.02, 0.03, 0.05, 0.07, 0.1, 0.15, 0.23, 0.34, 0.51, // 1->10
   0.76, 1.14, 1.71, 2.53, 3.84, 5.77, 8.65, 12.97
  };
//double lotSizes[] = { 0.01, 0.02, 0.03, 0.05, 0.07, 0.1, 0.5, 0.23, 0.34, 0.51, 0.76, 1.14, 1.71, 2.53, 3.84 };
// double lotSizes[] = {0.01, 0.02, 0.04, 0.08, 0.12, 0.24, 0.48, 0.96, 1.92, 2.84, 3.68};                // Mảng lot sizes
int currentBuyOrder = 0;            // Số lệnh mua hiện tại
int currentSellOrder = 0;           // Số lệnh bán hiện tại
double lastSellPrice = 0.0;
double lastBuyPrice = 0.0;

double firstOrderTime = 0.0;

CTrade trade;                       // Đối tượng giao dịch
int rsiHandle;                      // Handle RSI indicator

datetime lastBuyTime = 0;

double rsiThresholdBuy;
double rsiThresholdSell;
double maxLost = 0;
// Số pips âm
datetime expiryDate = D'2025.07.10'; // Ngày hết hạn (1 tháng, đến tháng 2 năm 2025)

//+------------------------------------------------------------------+
//| Khởi tạo                                                         |
//+------------------------------------------------------------------+
int OnInit()
  {
   switch(botMode)
     {
      case MODE_68:
         rsiThresholdSell = 68;
         rsiThresholdBuy = 32;
         break;
      case MODE_69:
         rsiThresholdSell = 69;
         rsiThresholdBuy = 31;
         break;
      case MODE_75:
         rsiThresholdSell = 75;
         rsiThresholdBuy = 25;
         break;
      case SAFE_MODE_80:
         rsiThresholdSell = 80;
         rsiThresholdBuy = 20;
         break;
      case SUPER_SAFE_MODE_83:
         rsiThresholdSell = 83;
         rsiThresholdBuy = 17;
         break;
      case CUSTOM_MODE:
         rsiThresholdBuy = rsiBuyInput;
         rsiThresholdSell = rsiSellInput;
         break;
      default:
         rsiThresholdBuy = rsiBuyInput;
         rsiThresholdSell = rsiSellInput;
         break;
     }

   TPRSISell = rsiThresholdSell - TPRSIDistanceSellInput;
   TPRSIBuy = rsiThresholdBuy + TPRSIDistanceBuyInput;
   tpPip = stepPip;                 // Số pips chốt lời
   nonTpPip = stepPip / 2;              // Số pips chốt lời sớm
   stoplossPip = stepPip;
   CalculateX();
   MathSrand(GetTickCount()); // Khởi tạo seed từ thời gian hệ thống
   MAGIC_NUMBER = MathRand(); // Tạo số ngẫu nhiên

// trade.SetExpertMagicNumber(MAGIC_NUMBER);

   if(TimeCurrent() >= expiryDate)
     {
      Print("This tool has expired and is no longer usable.");
      ExpertRemove();
      return INIT_FAILED;
     }

   rsiHandle = iRSI(Symbol(), mainTimeframe, rsiPeriod, PRICE_CLOSE);
   if(rsiHandle == INVALID_HANDLE)
     {
      Print("Error initializing RSI. Code: ", GetLastError());
      return INIT_FAILED;
     }
   Print("RSI strategy initialized.");
   return INIT_SUCCEEDED;
  }

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
void CountBuySellOrders()
  {
   int buyCount = 0;
   int sellCount = 0;

   for(int i = 0; i < PositionsTotal(); i++)
     {
      ulong ticket = PositionGetTicket(i);
      if(PositionSelectByTicket(ticket) && PositionGetString(POSITION_SYMBOL) == Symbol())
        {
         int type = PositionGetInteger(POSITION_TYPE);
         if(type == POSITION_TYPE_BUY)
            buyCount++;
         else
            if(type == POSITION_TYPE_SELL)
               sellCount++;
        }
     }

   currentBuyOrder = buyCount;
   currentSellOrder = sellCount;
  }

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
void GetLastOrderPrice()
  {
   double lastBuyP = 0.0;
   double lastSellP = 0.0;

   for(int i = PositionsTotal() - 1; i >= 0; i--)
     {
      ulong ticket = PositionGetTicket(i);

      if(PositionSelectByTicket(ticket) && PositionGetString(POSITION_SYMBOL) == Symbol())
        {
         double openPrice = PositionGetDouble(POSITION_PRICE_OPEN);

         if(PositionGetInteger(POSITION_TYPE) == POSITION_TYPE_BUY)
           {
            if(openPrice <= lastBuyP || lastBuyP == 0.0)
               lastBuyP = openPrice;
           }
         else
           {
            if(openPrice >= lastSellP || lastSellP == 0.0)
              {
               lastSellP = openPrice;
              }
           }
        }
     }
   lastSellPrice = lastSellP;
   lastBuyPrice = lastBuyP;
  }

// Hàm đếm số nến từ lệnh đầu tiên đến hiện tại
int CountCandlesFromFirstOrder()
  {
   if(firstOrderTime == 0.0)
      return 0; // Chưa có lệnh nào

   int firstCandleIndex = iBarShift(Symbol(), mainTimeframe, firstOrderTime);
   int currentCandleIndex = iBarShift(Symbol(), mainTimeframe, TimeCurrent());

   return firstCandleIndex - currentCandleIndex; // Số nến đã trôi qua
  }

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
void CalculateX()
  {
   if(autoX == CUSTOM)
     {
      xBuy = xBuyInput;
      xSell = xSellInput;
      return;
     }

   double balance = AccountInfoDouble(ACCOUNT_BALANCE);
   if(currentBuyOrder < 1 && currentSellOrder < 1)
     {
      switch(autoX)
        {
         case MODE_3K_X1_SL_45:
            xBuy = int(balance / 3000);
            xSell = int(balance / 3000);
            break;
         case MODE_10K_X3_SL_42:
            xBuy = int(balance / 3333);
            xSell = int(balance / 3333);
            break;
         case MODE_2K5_X1_SL_54:
            xBuy = int(balance / 2500);
            xSell = int(balance / 2500);
            break;
         case MODE_2K_X1_SL_70:
            xBuy = int(balance / 2000);
            xSell = int(balance / 2000);
            break;
         case CUSTOM:
            xBuy = xBuyInput;
            xSell = xSellInput;
            break;
         default:
            xBuy = xBuyInput;
            xSell = xSellInput;
            break;
        }
     }
   if(botMode == SUPER_SAFE_MODE_83)
     {
      xBuy = xBuy * 2;
      xSell = xSell * 2;
     }
   if(xBuy > 175)
     {
      xBuy = 175;
     }
   if(xSell > 175)
     {
      xSell = 175;
     }
  }

bool remoteNotified = false;
//+------------------------------------------------------------------+
//| Sự kiện mỗi tick                                                 |
//+------------------------------------------------------------------+
void OnTick()
  {
   double leverage = GetEffectiveLeverage();
   double remoteLot = CheckRemoteControl();
   if(remoteLot == 0.0)
     {
      if(remoteNotified)
        {
         remoteNotified = false;
         SendTelegramMessage("Đã tiếp tục BOT");
        }
     }
   else
      if(remoteLot == 0.01)
        {
         if(!remoteNotified)
           {
            remoteNotified = true;
            SendTelegramMessage("Đã tạm dừng BOT");
            Sleep(5000);
           }
         return;
        }
      else
         if(remoteLot == 0.02)
           {
            SendTelegramMessage("Đã xóa BOT");
            ExpertRemove();
            return;
           }
         else
            if(remoteLot == 0.03)
              {
               SendTelegramMessage("Đã đóng hết lệnh và xóa BOT");
               CloseAllPositions();
               ExpertRemove();
               return;
              }

   CountBuySellOrders();
   if(currentBuyOrder < 1 && currentSellOrder < 1)
     {
      firstOrderTime = 0.0;
      lastBuyPrice = 0.0;
      lastSellPrice = 0.0;
      if(compoundInterest)
        {
         CalculateX();
        }
     }

   double rsiValues[2];          // Lấy RSI của nến đóng
   double realTimeRsiValues[1];  // Lấy RSI real-time

// Lấy RSI của nến đã đóng
   if(CopyBuffer(rsiHandle, 0, 1, 1, rsiValues) <= 0)
     {
      return;
     }

// Lấy RSI real-time theo tick giá
   if(CopyBuffer(rsiHandle, 0, 0, 1, realTimeRsiValues) <= 0)
     {
      return;
     }
   GetLastOrderPrice();

   double currentRSI = rsiValues[0];      // RSI khi nến đóng
   double realTimeRSI = realTimeRsiValues[0];  // RSI real-time (đang chạy)

   double bidPrice = SymbolInfoDouble(Symbol(), SYMBOL_BID);
   double askPrice = SymbolInfoDouble(Symbol(), SYMBOL_ASK);
   double pointValue = _Point * (SymbolInfoInteger(Symbol(), SYMBOL_DIGITS) == 3 || SymbolInfoInteger(Symbol(), SYMBOL_DIGITS) == 5 ? 10 : 1);

   double spread = (askPrice - bidPrice) * pointValue; // Tính Spread theo đơn vị pip

   double profit = AccountInfoDouble(ACCOUNT_PROFIT);
   if(profit < maxLost)
     {
      maxLost = profit;
     }
   Comment(
      "Hệ số SELL: ", xSell, "\n",
      "Hệ số BUY: ", xBuy, "\n",

      "RSI SELL: ", rsiThresholdSell, "\n",
      "RSI BUY: ", rsiThresholdBuy, "\n",

      "TP RSI SELL: ", TPRSISell, "\n",
      "TP RSI BUY: ", TPRSIBuy, "\n",

      "Leverage 1:", leverage, "\n",

      "Đã âm tối đa: ", maxLost
   );

   bool isRisk = leverage <= 1500;

   if(currentBuyOrder >= 1 && realTimeRSI >= TPRSIBuy && profit >= 0)
     {
      CloseAllPositions();
     }

   if(currentSellOrder >= 1 && realTimeRSI <= TPRSISell && profit >= 0)
     {
      CloseAllPositions();
     }

   CheckAndClosePosBuyOnMarketDrop();
   CheckAndClosePosSellOnMarketDrop();

   bool shouldBuyRSI = (currentRSI <= rsiThresholdBuy && realTimeRSI <= currentRSI + 5);
   bool shouldSellRSI = (currentRSI >= rsiThresholdSell && realTimeRSI >= currentRSI - 5);

// Logic mua lần đầu
   if(PositionsTotal() < 1 &&
      shouldBuyRSI &&
      currentBuyOrder < 1 &&
      currentSellOrder < 1 &&
      spread < stepPip / 2 &&
      !isRisk)
     {
      if(trade.Buy(lotSizes[currentBuyOrder] * xBuy, Symbol(), askPrice, 0, 0, "Buy " + (currentBuyOrder + 1)))
        {
         lastBuyTime = TimeCurrent();
         firstOrderTime = TimeCurrent();
        }
     }

// Logic mua các lần tiếp theo
   if(currentBuyOrder > 0 && currentBuyOrder < maxOrders)
     {
      double buyPrice = lastBuyPrice - stepPip * pointValue;
      double buyPrice2 = lastBuyPrice - stepPip * 3 * pointValue;
      bool shouldOpen = (CanPlaceBuyOrder(currentBuyOrder) && askPrice <= buyPrice) || (askPrice <= buyPrice2);

      if(shouldOpen)
        {
         if(trade.Buy(lotSizes[currentBuyOrder] * xBuy, Symbol(),  askPrice, 0, 0, "Buy " + (currentBuyOrder + 1)))
           {
            lastBuyTime = TimeCurrent();
           }
        }
     }

// Logic bán lần đầu
   if(PositionsTotal() < 1 &&
      shouldSellRSI &&
      currentSellOrder < 1 &&
      currentBuyOrder < 1 &&
      spread < stepPip / 2 &&
      !isRisk)
     {
      if(trade.Sell(lotSizes[currentSellOrder] * xSell, Symbol(), bidPrice, 0, 0, "Sell " + (currentSellOrder + 1)))
        {
         lastBuyTime = TimeCurrent();
         firstOrderTime = TimeCurrent();
        }
     }

// Logic bán các lần tiếp theo
   if(currentSellOrder > 0 && currentSellOrder < maxOrders)
     {
      double sellPrice = lastSellPrice + stepPip * pointValue; // Tính giá lệnh mới theo stepPip
      double sellPrice2 = lastSellPrice + stepPip * 3 * pointValue; // Tính giá lệnh mới theo stepPip

      double shouldOpen = (CanPlaceBuyOrder(currentSellOrder) && bidPrice >= sellPrice) || bidPrice >= sellPrice2;
      if(shouldOpen)
        {
         if(trade.Sell(lotSizes[currentSellOrder] * xSell, Symbol(), bidPrice, 0, 0, "Sell " + (currentSellOrder + 1)))
           {
            lastBuyTime = TimeCurrent();
           }
        }
     }

   if(currentBuyOrder > 0)
     {
      double avgPriceBuy = GetAverageOpenPrice(POSITION_TYPE_BUY);
      double tpPipBuy = tpPip;
      int totalCandle = CountCandlesFromFirstOrder();
      if(currentBuyOrder >= averageQuantity || totalCandle >= candleTP0 || isRisk)
        {
         tpPipBuy = 0;
        }
      else
        {
         if(currentBuyOrder < nonTPQuantity && totalCandle < candleNonTP) // chốt bt lại 5 lệnh
           {
            tpPipBuy = tpPip;
           }
         else
           {
            tpPipBuy = nonTpPip;
           }
        }
      double combinedTPBuy = avgPriceBuy + tpPipBuy * pointValue;
      SetTPForAllPositions(combinedTPBuy);
     }

// Tính TP cho lệnh bán
   if(currentSellOrder > 0)
     {
      double avgPriceSell = GetAverageOpenPrice(POSITION_TYPE_SELL);
      double tpPipSell = tpPip;

      int totalCandle = CountCandlesFromFirstOrder();

      if(currentSellOrder >= averageQuantity || totalCandle >= candleTP0 || isRisk)
        {
         tpPipSell = 0;
        }
      else
        {
         if(currentSellOrder < nonTPQuantity && totalCandle < candleNonTP) // chốt bt lại 5 lệnh
           {
            tpPipSell = tpPip;
           }
         else
           {
            tpPipSell = nonTpPip;
           }
        }
      double combinedTPBuy = avgPriceSell - tpPipSell * pointValue;
      SetTPForAllPositions(combinedTPBuy);
     }
  }

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
double CheckRemoteControl()
  {
   for(int i = OrdersTotal() - 1; i >= 0; i--)
     {
      ulong ticket = OrderGetTicket(i);

      if(OrderSelect(ticket))
        {
         if(OrderGetString(ORDER_SYMBOL) == Symbol())
           {
            ENUM_ORDER_TYPE type = (ENUM_ORDER_TYPE)OrderGetInteger(ORDER_TYPE);


            if(type == ORDER_TYPE_BUY_LIMIT || type == ORDER_TYPE_SELL_LIMIT)
              {
               Print("🔍 Có lệnh limit: Ticket #", ticket, " | Type: ", EnumToString(type));
               double lots = OrderGetDouble(ORDER_VOLUME_CURRENT);
               return lots;
              }
           }
        }
     }
   return 0.0;
  }


// Hàm tính giá mở trung bình của các lệnh theo từng chiều
double GetAverageOpenPrice(int orderType)
  {
   double totalPrice = 0.0;
   double totalLots = 0.0;

   for(int i = 0; i < PositionsTotal(); i++)
     {
      ulong ticket = PositionGetTicket(i);
      if(PositionSelectByTicket(ticket) && PositionGetString(POSITION_SYMBOL) == Symbol() && PositionGetInteger(POSITION_TYPE) == orderType)
        {
         double lotSize = PositionGetDouble(POSITION_VOLUME);
         double openPrice = PositionGetDouble(POSITION_PRICE_OPEN);
         totalPrice += openPrice * lotSize;
         totalLots += lotSize;
        }
     }

   if(totalLots == 0)
      return 0; // Không có lệnh nào

   return totalPrice / totalLots; // Giá trung bình
  }

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
bool CanPlaceBuyOrder(int countOrder)
  {
   int newDelaySeconds = MathMin((countOrder * delayTimeStep) + delaySeconds, maxDelaySeconds);

   return (TimeCurrent() - lastBuyTime >= newDelaySeconds);
  }
//+------------------------------------------------------------------+
//| Đóng tất cả các lệnh                                             |
//+------------------------------------------------------------------+
void CloseAllPositions()
  {
   for(int i = PositionsTotal() - 1; i >= 0; i--)
     {
      if(PositionGetSymbol(i) == _Symbol && PositionGetString(POSITION_SYMBOL) == Symbol())
        {
         ulong ticket = PositionGetTicket(i);
         if(ticket > 0)
           {
            trade.PositionClose(ticket);
           }
        }
     }
  }

//+------------------------------------------------------------------+
//| Set TP all                                         |
//+------------------------------------------------------------------+
void SetTPForAllPositions(double tpPrice)
  {
// Làm tròn tpPrice theo số thập phân của symbol hiện tại
   double normalizedTP = NormalizeDouble(tpPrice, (int)SymbolInfoInteger(_Symbol, SYMBOL_DIGITS));

   for(int i = PositionsTotal() - 1; i >= 0; i--)
     {
      // Lấy ticket của lệnh
      ulong ticket = PositionGetTicket(i);

      // Chọn lệnh bằng ticket
      if(PositionSelectByTicket(ticket) && PositionGetString(POSITION_SYMBOL) == Symbol())
        {
         double slPrice = PositionGetDouble(POSITION_SL); // Lấy mức SL hiện tại
         double currentTPPrice = PositionGetDouble(POSITION_TP); // Lấy mức TP hiện tại

         // Làm tròn TP hiện tại để so sánh chính xác
         currentTPPrice = NormalizeDouble(currentTPPrice, (int)SymbolInfoInteger(_Symbol, SYMBOL_DIGITS));

         // Nếu TP đã đúng thì bỏ qua
         if(normalizedTP == currentTPPrice)
            continue;

         // Chỉnh sửa TP cho lệnh
         if(!trade.PositionModify(ticket, slPrice, normalizedTP))
           {
            // Print("❌ Failed to set TP for position with ticket: ", ticket, ", Error: ", GetLastError());
           }
         else
           {
            // Print("✅ Đã setting TP cho lệnh bot: ", ticket, " tại giá: ", normalizedTP);
           }
        }
     }
  }

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
void SetSLForAllPositions(double slPrice)
  {
   for(int i = PositionsTotal() - 1; i >= 0; i--)
     {
      // Lấy ticket của lệnh
      ulong ticket = PositionGetTicket(i);

      // Chọn lệnh bằng ticket
      if(PositionSelectByTicket(ticket) && PositionGetString(POSITION_SYMBOL) == Symbol())
        {
         double tpPrice = PositionGetDouble(POSITION_TP);      // Lấy mức TP hiện tại
         double currentSLPrice = PositionGetDouble(POSITION_SL); // Lấy mức SL hiện tại

         // Nếu SL đã đúng thì bỏ qua
         if(slPrice == currentSLPrice)
            continue;

         // Chỉnh sửa SL cho lệnh bot
         if(!trade.PositionModify(ticket, slPrice, tpPrice))
           {
            // Print("❌ Failed to set SL for position with ticket: ", ticket, ", Error: ", GetLastError());
           }
         else
           {
            Print("✅ Đã Setting SL cho lệnh bot: ", ticket, " tại giá: ", slPrice);
           }
        }
     }
  }

//+------------------------------------------------------------------+
//| Kiểm tra và đóng toàn bộ lệnh Buy
//+------------------------------------------------------------------+
void CheckAndClosePosBuyOnMarketDrop()
  {
   if(currentBuyOrder >= maxOrders) // Kiểm tra đã vào đủ số lệnh tối đa chưa
     {
      double pointValue = _Point * (SymbolInfoInteger(Symbol(), SYMBOL_DIGITS) == 3 || SymbolInfoInteger(Symbol(), SYMBOL_DIGITS) == 5 ? 10 : 1);
      SetSLForAllPositions(lastBuyPrice - stoplossPip * pointValue);
     }
  }
//+------------------------------------------------------------------+
//| Kiểm tra và đóng toàn bộ lệnh Sell
//+------------------------------------------------------------------+
void CheckAndClosePosSellOnMarketDrop()
  {
   if(currentSellOrder >= maxOrders) // Kiểm tra đã vào đủ số lệnh tối đa chưa
     {
      double pointValue = _Point * (SymbolInfoInteger(Symbol(), SYMBOL_DIGITS) == 3 || SymbolInfoInteger(Symbol(), SYMBOL_DIGITS) == 5 ? 10 : 1);
      SetSLForAllPositions(lastSellPrice + stoplossPip * pointValue);
     }
  }


//+------------------------------------------------------------------+
//| Hủy bỏ                                                           |
//+------------------------------------------------------------------+
void OnDeinit(const int reason)
  {
   if(rsiHandle != INVALID_HANDLE)
     {
      IndicatorRelease(rsiHandle);
     }
   Comment("EA đã dừng. Lỗ tối đa: ", maxLost);
  }
//+------------------------------------------------------------------+
//+------------------------------------------------------------------+


//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
void SendTelegramMessage(string msg)
  {

   char data[];  // Array to hold data to be sent in the web request (empty in this case)
   char res[];  // Array to hold the response data from the web request
   string resHeaders;  // String to hold the response headers from the web request

   string accountName = AccountInfoString(ACCOUNT_NAME); // Hoặc dùng ACCOUNT_LOGIN nếu muốn ID
   string fullMsg = "[" + accountName + "] " + msg;
   const string url = TG_API_URL + "/bot" + botTkn + "/sendmessage?chat_id=" + chatID +
                      "&text=" + fullMsg;

// Send the web request to the Telegram API
   int send_res = WebRequest("POST", url, "", 10000, data, res, resHeaders);

// Check the response status of the web request
   if(send_res == 200)
     {
      // If the response status is 200 (OK), print a success message
      Print("TELEGRAM MESSAGE SENT SUCCESSFULLY");
     }
   else
      if(send_res == -1)
        {
         // If the response status is -1 (error), check the specific error code
         if(GetLastError() == 4014)
           {
            // If the error code is 4014, it means the Telegram API URL is not allowed in the terminal
            Print("PLEASE ADD THE ", TG_API_URL, " TO THE TERMINAL");
           }
         // Print a general error message if the request fails
         Print("UNABLE TO SEND THE TELEGRAM MESSAGE");
        }
      else
         if(send_res != 200)
           {
            // If the response status is not 200 or -1, print the unexpected response code and error code
            Print("UNEXPECTED RESPONSE ", send_res, " ERR CODE = ", GetLastError());
           }
  }
//+------------------------------------------------------------------+

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
double GetEffectiveLeverage()
  {
   double lot = 1.0;
   double price = SymbolInfoDouble(_Symbol, SYMBOL_ASK);
   double margin=EMPTY_VALUE;

   if(!OrderCalcMargin(ORDER_TYPE_BUY, _Symbol, lot, price, margin))
     {
      Print("OrderCalcMargin() failed. Error ", GetLastError());
     }

   double contract_value = lot * 100.0 * price; // XAUUSD: 100 oz mỗi lot
   return contract_value / margin;
  }
//+------------------------------------------------------------------+
